#!/bin/tcsh -f

umask 002

if ( $?SKIP_MRIS_MAKE_SURFACES_TEST ) then
  exit 0
endif

set passed = 1

foreach threads ( 4 8 )

  # extract testing data
  
  rm -rf testdata
  
  gunzip -c testdata.tar.gz | tar xvf -

  cd testdata

  setenv FREESURFER_HOME ../../distribution
  setenv SUBJECTS_DIR `pwd`
  setenv OMP_NUM_THREADS $threads
  echo "testing with $threads thread(s)"

  setenv FREESURFER_REPLACEMENT_FOR_CREATION_TIME_STRING "Sun Jan 11 11:11:11 ZONE 2011"
  
  # ---- TEST 1 ----

  # run mris_make_surfaces using typical input

  if (1) then
  
    set cmd=(../mris_make_surfaces \
             -aseg aseg.presurf \
             -white white.preaparc -noaparc \
             -whiteonly -mgz -T1 brain.finalsurfs \
             subject lh)

    echo ""
    echo $cmd
    $cmd
    if ($status != 0) then
      echo "mris_make_surfaces FAILED"
      set passed = 0
    else
      # compare expected results with actual (produced) results

      set TST = subject/surf/lh.white.preaparc
      set REF = ${TST}.REF
      set cmd=(../../mris_diff/mris_diff --debug $REF $TST );

      echo ""
      echo $cmd
      $cmd
      set diff_status=$status
      if ($diff_status != 0) then
        echo "$cmd FAILED (exit status=$diff_status)"
        set passed = 0
      endif
    endif

  endif


  # ---- TEST 2 ----

  # run mris_make_surfaces using typical input

  if (1) then
  
    set cmd=(../mris_make_surfaces \
             -orig_white white.preaparc -orig_pial white.preaparc \
             -aseg aseg.presurf -mgz -T1 brain.finalsurfs \
             subject lh)

    echo ""
    echo $cmd
    $cmd
    if ($status != 0) then
      echo "mris_make_surfaces FAILED"
      set passed = 0
    else
      # compare expected results with actual (produced) results

      foreach surface ( white pial )

        set TST = subject/surf/lh.$surface
        set REF = ${TST}.REF
        set cmd=(../../mris_diff/mris_diff --debug $REF $TST );

        echo ""
        echo $cmd
        $cmd
        set diff_status=$status
        if ($diff_status != 0) then
          echo "$cmd FAILED (exit status=$diff_status)"
          set passed = 0
        endif

      end
    endif
  endif
  
  echo ""
  echo ""
  echo ""


  # cleanup

  cd ..

  # leave the files for making a replacement .tar.gz
  #     rm -rf testdata

end

if ($passed != 0) then
  echo ""
  echo "test_mris_make_surfaces passed all tests"
  exit 0
else
  echo ""
  echo "test_mris_make_surfaces failed some tests"
  exit 1
endif
