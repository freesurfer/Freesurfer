#!/bin/tcsh -f

#
# test_mri_coreg
#
# execute mri_coregt and check results against expected results
#
# Original Author: Doug Greve
# CVS Revision Info:
#    $Author: zkaufman $
#    $Date: 2016/09/26 18:09:03 $
#    $Revision: 1.2 $
#
# Copyright Â© 2011 The General Hospital Corporation (Boston, MA) "MGH"
#
# Terms and conditions for use, reproduction, distribution and contribution
# are found in the 'FreeSurfer Software License Agreement' contained
# in the file 'LICENSE' found in the FreeSurfer distribution, and here:
#
# https://surfer.nmr.mgh.harvard.edu/fswiki/FreeSurferSoftwareLicense
#
# Reporting: freesurfer@nmr.mgh.harvard.edu
#
# General inquiries: freesurfer@nmr.mgh.harvard.edu
#

set VERSION='$Id: test_mri_coreg,v 1.2 2016/09/26 18:09:03 zkaufman Exp $';

umask 002

#
# extract testing data
#
setenv FREESURFER_HOME ../distribution
setenv SUBJECTS_DIR /no/such/directory


foreach threads ( 4 )
    rm -rf test_mri_coreg_cout_cerr_T${threads}.txt

    rm -Rf testdata
    tar xvfz testdata.tar.gz

    setenv OMP_NUM_THREADS $threads
    echo "testing with $threads thread(s)"

    # Run mri_coreg with really low tolerances so that it
    # runs faster. If using multiple threads, then
    # can pass  --threads NTHREADS
    set cmd = (./mri_coreg --mov testdata/template.nii.gz \
      --targ testdata/orig.mgz --reg testdata/reg.lta \
      --dof 12 --ftol .1 --linmintol .1)
    echo ""
    echo $cmd
    $cmd |& tee test_mri_coreg_cout_cerr_T${threads}.txt
    if($status) then
      echo "mri_coreg FAILED on execution"
      exit 1
    endif

    # Strip out hashtags so can do the diff
    grep -v \# testdata/source.lta > testdata/source.lta.strip
    grep -v \# testdata/reg.lta > testdata/reg.lta.strip
    set n = `diff testdata/reg.lta.strip testdata/source.lta.strip | wc -l`
    if($n != 0) then
      echo "mri_coreg FAILED to produce consistent results"
      exit 1
    endif

    #
    # cleanup
    #
    rm -Rf testdata

end

echo ""
echo "test_mri_coreg passed all tests"

exit 0
