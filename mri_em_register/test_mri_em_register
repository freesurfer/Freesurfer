#!/bin/tcsh -f

#
# test_mri_em_register
#
# execute mri_em_register and check results against expected results
#
# Original Author: Nick Schmansky
# CVS Revision Info:
#    $Author: zkaufman $
#    $Date: 2016/07/28 14:17:45 $
#    $Revision: 1.6 $
#
# Copyright Â© 2014 The General Hospital Corporation (Boston, MA) "MGH"
#
# Terms and conditions for use, reproduction, distribution and contribution
# are found in the 'FreeSurfer Software License Agreement' contained
# in the file 'LICENSE' found in the FreeSurfer distribution, and here:
#
# https://surfer.nmr.mgh.harvard.edu/fswiki/FreeSurferSoftwareLicense
#
# Reporting: freesurfer@nmr.mgh.harvard.edu
#
# General inquiries: freesurfer@nmr.mgh.harvard.edu
#

set VERSION='$Id: test_mri_em_register,v 1.6 2016/07/28 14:17:45 zkaufman Exp $'

umask 002

# backdoor bypass:
if ( $?SKIP_MRI_EM_REGISTER_TEST ) then
  echo "skipping test_mri_em_register"
  exit 77
endif

#
# extract testing data
#
gunzip -c testdata.tar.gz | tar xvf -

#
# run mri_em_register using typical input file formats,
# first with one thread then with 8 threads.
#

cd testdata

set threads=( 1 8 )
if ( $?SKIP_MRI_EMREG_1CPU_TEST ) set threads=( 8 )

foreach num ($threads)

    setenv OMP_NUM_THREADS $num
    set cmd=(../mri_em_register \
        -uns 3 \
        -mask brainmask.mgz \
        nu.mgz \
        ../../distribution/average/RB_all_2016-05-10.vc700.gca \
        talairach.${num}cpu.lta)
    $cmd
    if ($status != 0) then
        echo "mri_em_register FAILED"
    exit 1
    endif

end

#
# compare expected results with actual (produced) results
#

foreach num ($threads)

  set cmd=(../../mri_robust_register/lta_diff \
    talairach.lta talairach.${num}cpu.lta);
  $cmd
  if ($status != 0) then
    echo "$cmd FAILED (RMS=$result)"
    exit 1
  endif

end

#
# cleanup
#
cd ..
rm -Rf testdata

echo "" 
echo "test_mri_em_register passed all tests"
exit 0
