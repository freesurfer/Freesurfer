#!/bin/tcsh -f

umask 002

if ( $?SKIP_MRI_RELABEL_HYPOINTENSITIES_TEST ) then
  exit 0
endif

set passed = 1

foreach threads ( 4 )

  # extract testing data
  
  rm -rf testdata
  
  gunzip -c testdata.tar.gz | tar xvf -

  cd testdata

  setenv FREESURFER_HOME ../../distribution
  setenv SUBJECTS_DIR `pwd`
  setenv OMP_NUM_THREADS $threads
  echo "testing with $threads thread(s)"


  # ---- TEST 1 ----

  # run mri_relabel_hypointensities using typical input

  set cmd=(../mri_relabel_hypointensities aseg.presurf.mgz ./surf aseg.presurf.hypos.mgz )

  echo ""
  echo $cmd
  $cmd
  if ($status != 0) then
    echo "mris_make_surfaces FAILED"
    set passed = 0
  else
    # compare expected results with actual (produced) results

    set TST = aseg.presurf.hypos.mgz
    rm -rf ${TST}_tmp{,.gz}
    rm -rf ${TST}.REF_tmp{,.gz}
    cp     ${TST}{,_tmp.gz}
    cp     ${TST}.REF{,_tmp.gz}
    gunzip ${TST}_tmp.gz
    gunzip ${TST}.REF_tmp.gz
    
    #hexdump -c <  ${TST}_tmp     | wc
    #hexdump -c <  ${TST}.REF_tmp | wc
    hexdump -c <  ${TST}_tmp     | head -n-125 > ${TST}_hexdump.txt
    hexdump -c <  ${TST}.REF_tmp | head -n-125 > ${TST}.REF_hexdump.txt
    #cksum ${TST}_hexdump.txt ${TST}.REF_hexdump.txt
    
    set cmd=(diff -q ${TST}_hexdump.txt ${TST}.REF_hexdump.txt );

    echo ""
    echo $cmd
    $cmd
    set diff_status=$status
    if ($diff_status != 0) then
      echo "$cmd FAILED (exit status=$diff_status)"
      set passed = 0
    endif
  endif


  echo ""
  echo ""
  echo ""


  # cleanup

  cd ..
  # leave the files for making a replacement .tar.gz
  #     rm -rf testdata

end

if ($passed != 0) then
  echo ""
  echo "test_mri_relabel_hypointensities passed all tests"
  exit 0
else
  echo ""
  echo "test_mri_relabel_hypointensities failed some tests"
  exit 1
endif
